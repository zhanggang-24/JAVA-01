## 1.Hello.java
	package Hello;
	public class Hello {
	    public static void main(String[] args) {
	        long sum = 0L;
	        int count = 10;
	        Boolean result = false;
	        for (int i = 0; i < count; i++) {
	            sum = sum + (i * 2 + 1);
	        }
	        if (sum >= 50) {
	            result = true;
	        }
	        System.out.println("result=" + result);
	    }
	}
## 2.执行命令
	javac -g Hello.java
	javap -c -v Hello
## 3.命令解释
	javac -help
		  -g                         生成所有调试信息
		  -g:none                    不生成任何调试信息
		  -g:{lines,vars,source}     只生成某些调试信息
		  -nowarn                    不生成任何警告
		  -verbose                   输出有关编译器正在执行的操作的消息
		  -deprecation               输出使用已过时的 API 的源位置
		  -classpath <路径>            指定查找用户类文件和注释处理程序的位置
		  -cp <路径>                   指定查找用户类文件和注释处理程序的位置
		  -sourcepath <路径>           指定查找输入源文件的位置
		  -bootclasspath <路径>        覆盖引导类文件的位置
		  -extdirs <目录>              覆盖所安装扩展的位置
		  -endorseddirs <目录>         覆盖签名的标准路径的位置
		  -proc:{none,only}          控制是否执行注释处理和/或编译。
		  -processor <class1>[,<class2>,<class3>...] 要运行的注释处理程序的名称; 绕过默认的搜索进程
		  -processorpath <路径>        指定查找注释处理程序的位置
		  -parameters                生成元数据以用于方法参数的反射
		  -d <目录>                    指定放置生成的类文件的位置
		  -s <目录>                    指定放置生成的源文件的位置
		  -h <目录>                    指定放置生成的本机标头文件的位置
		  -implicit:{none,class}     指定是否为隐式引用文件生成类文件
		  -encoding <编码>             指定源文件使用的字符编码
		  -source <发行版>              提供与指定发行版的源兼容性
		  -target <发行版>              生成特定 VM 版本的类文件
		  -profile <配置文件>            请确保使用的 API 在指定的配置文件中可用
		  -version                   版本信息
		  -help                      输出标准选项的提要
		  -A关键字[=值]                  传递给注释处理程序的选项
		  -X                         输出非标准选项的提要
		  -J<标记>                     直接将 <标记> 传递给运行时系统
		  -Werror                    出现警告时终止编译
		  @<文件名>                     从文件读取选项和文件名

	javap -help
		  -help  --help  -?        输出此用法消息
		  -version                 版本信息
		  -v  -verbose             输出附加信息
		  -l                       输出行号和本地变量表
		  -public                  仅显示公共类和成员
		  -protected               显示受保护的/公共类和成员
		  -package                 显示程序包/受保护的/公共类
		                           和成员 (默认)
		  -p  -private             显示所有类和成员
		  -c                       对代码进行反汇编
		  -s                       输出内部类型签名
		  -sysinfo                 显示正在处理的类的
		                           系统信息 (路径, 大小, 日期, MD5 散列)
		  -constants               显示最终常量
		  -classpath <path>        指定查找用户类文件的位置
		  -cp <path>               指定查找用户类文件的位置
		  -bootclasspath <path>    覆盖引导类文件的位置
## 4字节码分析
	“//”表示分析内容

	警告: 二进制文件Hello包含Hello.Hello
	//类的描述信息
	Classfile /E:/JAVA/homework/HelloClassLoader/src/Hello/Hello.class
	  Last modified 2021-1-10; size 1053 bytes
	  MD5 checksum f97c5fb36bae4f94367e2de95db5a5d9
	  Compiled from "Hello.java"
	public class Hello.Hello
	  minor version: 0
	//52表示使用jdk8编译
	  major version: 52
	  flags: ACC_PUBLIC, ACC_SUPER
	//常量池
	Constant pool:
	   #1 = Methodref          #14.#38        // java/lang/Object."<init>":()V
	   #2 = Methodref          #39.#40        // java/lang/Boolean.valueOf:(Z)Ljava/lang/Boolean;
	   #3 = Long               50l
	   #5 = Fieldref           #41.#42        // java/lang/System.out:Ljava/io/PrintStream;
	   #6 = Class              #43            // java/lang/StringBuilder
	   #7 = Methodref          #6.#38         // java/lang/StringBuilder."<init>":()V
	   #8 = String             #44            // result=
	   #9 = Methodref          #6.#45         // java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
	  #10 = Methodref          #6.#46         // java/lang/StringBuilder.append:(Ljava/lang/Object;)Ljava/lang/StringBuilder;
	  #11 = Methodref          #6.#47         // java/lang/StringBuilder.toString:()Ljava/lang/String;
	  #12 = Methodref          #48.#49        // java/io/PrintStream.println:(Ljava/lang/String;)V
	  #13 = Class              #50            // Hello/Hello
	  #14 = Class              #51            // java/lang/Object
	  #15 = Utf8               <init>
	  #16 = Utf8               ()V
	  #17 = Utf8               Code
	  #18 = Utf8               LineNumberTable
	  #19 = Utf8               LocalVariableTable
	  #20 = Utf8               this
	  #21 = Utf8               LHello/Hello;
	  #22 = Utf8               main
	  #23 = Utf8               ([Ljava/lang/String;)V
	  #24 = Utf8               i
	  #25 = Utf8               I
	  #26 = Utf8               args
	  #27 = Utf8               [Ljava/lang/String;
	  #28 = Utf8               sum
	  #29 = Utf8               J
	  #30 = Utf8               count
	  #31 = Utf8               result
	  #32 = Utf8               Ljava/lang/Boolean;
	  #33 = Utf8               StackMapTable
	  #34 = Class              #27            // "[Ljava/lang/String;"
	  #35 = Class              #52            // java/lang/Boolean
	  #36 = Utf8               SourceFile
	  #37 = Utf8               Hello.java
	  #38 = NameAndType        #15:#16        // "<init>":()V
	  #39 = Class              #52            // java/lang/Boolean
	  #40 = NameAndType        #53:#54        // valueOf:(Z)Ljava/lang/Boolean;
	  #41 = Class              #55            // java/lang/System
	  #42 = NameAndType        #56:#57        // out:Ljava/io/PrintStream;
	  #43 = Utf8               java/lang/StringBuilder
	  #44 = Utf8               result=
	  #45 = NameAndType        #58:#59        // append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
	  #46 = NameAndType        #58:#60        // append:(Ljava/lang/Object;)Ljava/lang/StringBuilder;
	  #47 = NameAndType        #61:#62        // toString:()Ljava/lang/String;
	  #48 = Class              #63            // java/io/PrintStream
	  #49 = NameAndType        #64:#65        // println:(Ljava/lang/String;)V
	  #50 = Utf8               Hello/Hello
	  #51 = Utf8               java/lang/Object
	  #52 = Utf8               java/lang/Boolean
	  #53 = Utf8               valueOf
	  #54 = Utf8               (Z)Ljava/lang/Boolean;
	  #55 = Utf8               java/lang/System
	  #56 = Utf8               out
	  #57 = Utf8               Ljava/io/PrintStream;
	  #58 = Utf8               append
	  #59 = Utf8               (Ljava/lang/String;)Ljava/lang/StringBuilder;
	  #60 = Utf8               (Ljava/lang/Object;)Ljava/lang/StringBuilder;
	  #61 = Utf8               toString
	  #62 = Utf8               ()Ljava/lang/String;
	  #63 = Utf8               java/io/PrintStream
	  #64 = Utf8               println
	  #65 = Utf8               (Ljava/lang/String;)V

	{
	//方法的信息
	//默认构造函数
	  public Hello.Hello();
		//方法描述符，入参为空，返回值为void
	    descriptor: ()V
		//方法的访问权限：public
	    flags: ACC_PUBLIC
	    Code:
		  //操作数栈大小为1，局部变量表数组大小为1，参数个数为1
	      stack=1, locals=1, args_size=1
	         0: aload_0
	         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
	         4: return
	      LineNumberTable:
	        line 3: 0
		  //本地变量表
		  //Start:表示参数开始作用的位置（上述Code中前面的数字编号）
		  //Length:表示参数作用的范围（5表示该参数在整个方法范围内起作用 0~4）
		  //Slot:参数槽的编号
		  //Name:参数名称（非静态方法，第一个参数默认是this，分配在第0号槽中）
	      LocalVariableTable:
	        Start  Length  Slot  Name   Signature
	            0       5     0  this   LHello/Hello;
	//main方法
	  public static void main(java.lang.String[]);
		//方法描述符
		  //其中小括号内是入参信息/形参信息
		  //左方括号表述数组
		  //L 表示对象
		  //java/lang/String 就是类名称
		  //小括号后面的 V 则表示这个方法的返回值是 void
	    descriptor: ([Ljava/lang/String;)V
		//访问权限：public，static
	    flags: ACC_PUBLIC, ACC_STATIC
	    Code:
		  //操作数栈大小为4，局部变量表数组大小为6，参数个数为1
	      stack=4, locals=6, args_size=1
			//long类型0值入操作数栈
	         0: lconst_0
			//出栈，将0存储到局部变量表槽1的位置
	         1: lstore_1
			//入栈，valuebyte值(10)带符号扩展成int值入栈
	         2: bipush        10
			//出栈，将10存储在局部变量表槽3的位置
	         4: istore_3
			//入栈， int类型0入操作数栈
	         5: iconst_0
			//入栈，调用静态方法，在上述常量池中找 #2 的方法定义，返回的方法引用入操作数栈
	         6: invokestatic  #2                  // Method java/lang/Boolean.valueOf:(Z)Ljava/lang/Boolean;
			//出栈，将栈顶引用类型（上述静态方法产生）存储在局部变量表槽4的位置
	         9: astore        4
			//int类型的0入操作数栈
	        11: iconst_0
			//出栈，将栈顶元素存储到局部变量表槽5的位置
	        12: istore        5
			//入栈，将局部变量表槽5位置的数据入栈
	        14: iload         5
			//入栈，将局部变量表槽3位置的数据入栈
	        16: iload_3
			//栈顶的两个元素比较，若前大于等于后则跳转到36,执行完两个参数出栈，分别放到局部变量表槽3和槽5的位置
	        17: if_icmpge     36
			//入栈，将局部变量槽1中的数据入栈
	        20: lload_1
			//入栈，将局部变量表槽5位置的数据入栈
	        21: iload         5
			//入栈，将值2入栈
	        23: iconst_2
			//将栈顶两个int类型数相乘，结果入栈
	        24: imul
			//入栈，将值1入栈
	        25: iconst_1
			//将栈顶的两个int类型值相加，结果入栈
	        26: iadd
			//将栈顶int类型转换为long类型
	        27: i2l
			//将栈顶两long类型数相加，结果入栈
	        28: ladd
			//出栈，将栈顶long类型值保存到局部变量槽1中
	        29: lstore_1
			//局部变量槽5中的值加1
	        30: iinc          5, 1
			//跳转到上述编号14的位置
	        33: goto          14
			//入栈，将局部变量槽1中的数据入栈
	        36: lload_1
			//常量池中，编号#3的值（long 50L）入栈
	        37: ldc2_w        #3                  // long 50l
			//比较栈顶两long类型值，前者大，1入栈；相等，0入栈；后者大，-1入栈
	        40: lcmp
			//若栈顶int类型值小于0则跳转到编号50的位置
	        41: iflt          50
			//入栈，int类型的1入栈
	        44: iconst_1
			//入栈，调用静态方法，在上述常量池中找 #2 的方法定义，返回的方法引用入操作数栈
	        45: invokestatic  #2                  // Method java/lang/Boolean.valueOf:(Z)Ljava/lang/Boolean;
			//出栈，将栈顶引用类型（上述静态方法产生）存储在局部变量表槽4的位置
	        48: astore        4
			//获取常量池中#5的静态字段的值
	        50: getstatic     #5                  // Field java/lang/System.out:Ljava/io/PrintStream;
			//创建对象，常量池#6处的对象：StringBuilder
	        53: new           #6                  // class java/lang/StringBuilder
			//复制栈顶的引用值
	        56: dup
			//执行对象初始化
	        57: invokespecial #7                  // Method java/lang/StringBuilder."<init>":()V
			//常量池中，#8位置的值入栈
	        60: ldc           #8                  // String result=
			//调用StringBuilder 的append方法（string类型）
	        62: invokevirtual #9                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
			//局部变量表中槽4的引用类型入栈
	        65: aload         4
			//调用StringBuilder 的append方法（object类型）
	        67: invokevirtual #10                 // Method java/lang/StringBuilder.append:(Ljava/lang/Object;)Ljava/lang/StringBuilder;
			//调用StringBuilder 的toString方法（object类型）
	        70: invokevirtual #11                 // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
			//调用PrintStream的println方法
	        73: invokevirtual #12                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
			//返回
	        76: return
		//Hello.java的代码行数与上述字节码编号的映射关系
	      LineNumberTable:
	        line 5: 0
	        line 6: 2
	        line 7: 5
	        line 8: 11
	        line 9: 20
	        line 8: 30
	        line 11: 36
	        line 12: 44
	        line 14: 50
	        line 15: 76
		//局部变量表
	      LocalVariableTable:
	        Start  Length  Slot  Name   Signature
	           14      22     5     i   I
	            0      77     0  args   [Ljava/lang/String;
			//sum,是long类型的，会占两个槽，即槽1和槽2
	            2      75     1   sum   J
	            5      72     3 count   I
	           11      66     4 result   Ljava/lang/Boolean;
		//栈映射关系表
	      StackMapTable: number_of_entries = 3
	        frame_type = 255 /* full_frame */
	          offset_delta = 14
	          locals = [ class "[Ljava/lang/String;", long, int, class java/lang/Boolean, int ]
	          stack = []
	        frame_type = 250 /* chop */
	          offset_delta = 21
	        frame_type = 13 /* same */
	}
	SourceFile: "Hello.java"
